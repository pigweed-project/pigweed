# Copyright 2025 The Pigweed Authors
#
# Licensed under the Apache License, Version 2.0 (the "License"); you may not
# use this file except in compliance with the License. You may obtain a copy of
# the License at
#
#     https://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
# WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
# License for the specific language governing permissions and limitations under
# the License.

import("//build_overrides/pigweed.gni")

import("$dir_pw_build/target_types.gni")
import("$dir_pw_unit_test/test.gni")

config("public_include_path") {
  include_dirs = [ "public" ]
  visibility = [ ":*" ]
}

config("test_config") {
  defines = [ "PW_MULTIBUF_VERSION=2" ]
  visibility = [ ":*" ]
}

### Libraries and tests

# LINT.IfChange

pw_source_set("properties") {
  public_configs = [ ":public_include_path" ]
  public = [ "public/pw_multibuf/v2/properties.h" ]
}

pw_source_set("iterators") {
  public_configs = [ ":public_include_path" ]
  public = [
    "public/pw_multibuf/v2/chunks.h",
    "public/pw_multibuf/v2/internal/byte_iterator.h",
    "public/pw_multibuf/v2/internal/chunk_iterator.h",
    "public/pw_multibuf/v2/internal/entry.h",
  ]
  public_deps = [
    "$dir_pw_assert:assert",
    "$dir_pw_containers:dynamic_deque",
    dir_pw_bytes,
    dir_pw_preprocessor,
  ]
  visibility = [ "$dir_pw_multibuf/*" ]
}

pw_source_set("iterator_testing") {
  public_configs = [ ":public_include_path" ]
  public = [ "pw_multibuf_private/iterator_testing.h" ]
  public_deps = [
    ":iterators",
    "$dir_pw_allocator:bump_allocator",
    "$dir_pw_containers:storage",
    dir_pw_unit_test,
  ]
  visibility = [ ":*" ]
}

pw_test("chunk_iterator_test") {
  sources = [ "chunk_iterator_test.cc" ]
  configs = [ ":test_config" ]
  deps = [
    ":iterator_testing",
    ":iterators",
  ]
}

pw_test("byte_iterator_test") {
  sources = [ "byte_iterator_test.cc" ]
  configs = [ ":test_config" ]
  deps = [
    ":iterator_testing",
    ":iterators",
  ]
}

# LINT.ThenChange(BUILD.bazel, CMakeLists.txt)

# LINT.IfChange
pw_source_set("multibuf") {
  public_configs = [ ":public_include_path" ]
  public = [
    "public/pw_multibuf/v2/multibuf.h",
    "public/pw_multibuf/v2/observer.h",
  ]
  public_deps = [
    ":iterators",
    ":properties",
    "$dir_pw_allocator:null_allocator",
    "$dir_pw_containers:algorithm",
    "$dir_pw_containers:dynamic_deque",
    dir_pw_allocator,
    dir_pw_bytes,
  ]
  sources = [ "generic_multibuf.cc" ]
  deps = [ "$dir_pw_assert:check" ]
}

# LINT.ThenChange(../Android.bp, BUILD.bazel, CMakeLists.txt)

# LINT.IfChange
pw_test("multibuf_test") {
  sources = [ "multibuf_test.cc" ]
  configs = [ ":test_config" ]
  deps = [
    ":iterators",
    "$dir_pw_allocator:chunk_pool",
    "$dir_pw_allocator:testing",
    "$dir_pw_assert:check",
    dir_pw_bytes,
    dir_pw_multibuf,
    dir_pw_result,
    dir_pw_status,
  ]
  negative_compilation_tests = true
}

# LINT.ThenChange(BUILD.bazel, CMakeLists.txt)

## Test group

pw_test_group("tests") {
  tests = [
    ":byte_iterator_test",
    ":chunk_iterator_test",
    ":multibuf_test",
  ]
}
